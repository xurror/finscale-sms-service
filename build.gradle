buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.3.1.RELEASE',
                  'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: "com.github.hierynomus.license"

if (project.hasProperty('profile')
        && project.getProperty('profile') == 'deployable') {
    apply from: 'profile-deployable.gradle'
} else {
    apply from: 'profile-standalone.gradle'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/libs-release" }
    maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }
}

license {
    header rootProject.file('./LICENSE.md')
    excludes(["**/*.html", "**/*.mustache", "**/package-info.java", "**/keystore.jks"])
    strictCheck true
}

task licenseFormatBuildScripts (type:nl.javadude.gradle.plugins.license.License) {
    source = fileTree(dir: './', includes: ['**/*.bat', '**/*.sh', '**/*.sql'])
}
licenseFormat.dependsOn licenseFormatBuildScripts

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.flywaydb:flyway-core")
    compile('org.apache.velocity:velocity:1.7')
    compile('com.squareup.okhttp:okhttp:2.0.0')
    compile('com.squareup.okhttp:okhttp-urlconnection:2.0.0')
    compile('com.squareup.retrofit:retrofit:1.6.1')
    compile('com.twilio.sdk:twilio:7.1.0')
    compile('org.drizzle.jdbc:drizzle-jdbc:1.3')
    compile('com.infobip:infobip-api-java-client:2.1.0')
    testCompile("org.springframework.boot:spring-boot-starter-test")

}


